from snakemake.utils import min_version
min_version("7.0")

include: "rules/common.smk"

# load sample tsv file
# has header line: first must be "sample_accession"
# required columns (again tab separated):
#	sample_accession (SRS1234)
#	run_accession (SRR1234)
#	reference (hs111) # corresponds to the references.tsv file
# 	tech (10xv2, BULK, etc)
#	umi (yes / no)
#   workflow (standard, lammano, nac)
# 	everything else is optional after this
# you absolutely can process the same sample in multiple ways
# e.g. SRS1234 macaque_ref standard_workflow and SRS1234 human_ref nac_workflow
srr_sample_file = config['srr_sample_file']
SRS_dict = sample_dict_builder(srr_sample_file)
sample_names = list(SRS_dict.keys())

# load ref table
# used to match shorthand (hs111) to full ensembl/refseq/whatever gtf and fasta
# no header
# example hs111 <tab> http://.../Homo_sapiens.GRCh38.111.gtf.gz <tab> http://.../Homo_sapiens.GRCh38.111.fa.gz
ref_file = config['ref_file']
ref_dict = ref_builder(ref_file)

fastq_path = config['fastq_path']
quant_path = config['quant_path']


adata_umi_outputs, adata_bulk_outputs =  make_core_outputs(srr_sample_file, quant_path)

	
rule all:
	input:
		[l + '/matrix.ec' for l in adata_bulk_outputs],
		#[l + '/raw_gene_bc_matrices/matrix.mtx' for l in adata_umi_outputs],
		[l + '/final/QC.adata.solo.h5ad' for l in adata_umi_outputs],
		[l + '/final/QC.seuratV5.Rdata'  for l in adata_umi_outputs]

include: "rules/kb.smk"
include: "rules/convert_mtx.smk"

if config['decontamination'] == 'cellbender':
	include: "rules/cellbender.smk"

if config['decontamination'] == 'decontX':
	include: "rules/decontX.smk"

include: "rules/make_descriptive_gene_names.smk"
include: "rules/qc_filtering.smk"
include: "rules/solo.smk"
